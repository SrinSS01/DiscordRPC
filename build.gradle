plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.9'
    id "org.beryx.jlink" version "2.22.3"
}

group 'me.srinjoy'
version '1.0'

//jar {
//    manifest {
//        attributes 'Main-class': 'me.srinjoy.Start'
//    }
//    from {
//        configurations.runtimeClasspath.collect {
//            it.isDirectory() ? it : zipTree(it)
//        }
//    }
//}

jlink {
    launcher {
        name = 'DiscordRPC'
    }
    jpackage {
        installerType = project.findProperty('installerType')
        switch (installerType) {
            case 'msi':
                imageOptions += ['--icon', 'src/main/resources/me/srinjoy/logo.ico']
                installerOptions += [
                        '--win-per-user-install', '--win-dir-chooser',
                        '--win-menu', '--win-shortcut'
                ]
                break
            case 'pkg':
                imageOptions += ['--icon', 'src/main/resources/me/srinjoy/logo.icns']
                break
            case 'deb': case 'rpm':
                imageOptions += ['--icon', 'src/main/resources/me/srinjoy/logo.png']
                installerOptions += ['--linux-shortcut']
        }
    }
}

jpackage {
    doFirst {
        project.getProperty('installerType') // throws exception if its missing
    }
}

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    implementation 'club.minnced:java-discord-rpc:2.0.2'
    implementation ('com.googlecode.json-simple:json-simple:1.1.1') {
        exclude group: 'org.hamcrest'
    }
}
javafx {
    version = '15.0.1'
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}
mainClassName = 'me.srinjoy.Main'